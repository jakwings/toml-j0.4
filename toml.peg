Expressions
    = ( Whitespace / Newline / Comment )*
      ( Expression
        ( Whitespace / Comment )*
        ( Newline Expressions )? )?

Expression
    = TableArrayHeader
    / TableHeader
    / KeyValue

Newline                                         "Newline"
    = "\n" / "\r\n"

Whitespace                                      "Whitespace"
    = [ \t]

Comment                                         "Comment"
    = "#" ( !Newline . )*

KeyValue
    = Key Whitespace* "=" Whitespace* Value

Key
    = BareKey
    / QuotedKey

BareKey
    = [a-zA-Z0-9\-_]+

QuotedKey
    = DoubleQuote BasicCharacter+ DoubleQuote

DoubleQuote                                     "DoubleQuote"
    = '"'

SingleQuote                                     "SingleQuote"
    = "'"

ThreeDoubleQuotes                               "ThreeDoubleQuotes"
    = '"""'

ThreeSingleQuotes                               "ThreeSingleQuotes"
    = "'''"

Value
    = String
    / Boolean
    / DateTime
    / Float
    / Integer
    / Array
    / InlineTable

String
    = MultilineBasicString
    / BasicString
    / MultilineLiteralString
    / LiteralString

BasicString
    = DoubleQuote BasicCharacter* DoubleQuote

BasicCharacter
    = NormalCharacter
    / EscapedCharacter

NormalCharacter                                 "NormalCharacter"
    = !Newline [^"\\]

EscapedCharacter
    = Backslash ( ControlCharacter
                / DoubleQuote
                / Backslash
                / "u" FourHexadecimalDigits
                / "U" EightHexadecimalDigits )

ControlCharacter                                '"b", "f", "n", "r", "t"'
    = [bfnrt]

Backslash                                       "Backslash"
    = "\\"

FourHexadecimalDigits                           "FourHexadecimalDigits"
    = HexDigit HexDigit HexDigit HexDigit

EightHexadecimalDigits                          "EightHexadecimalDigits"
    = HexDigit HexDigit HexDigit HexDigit
      HexDigit HexDigit HexDigit HexDigit

HexDigit
    = [0-9A-Fa-f]

LiteralString
    = SingleQuote LiteralCharacter* SingleQuote

LiteralCharacter                                "NonSingleQuoteCharacter"
    = !Newline [^']

MultilineBasicString
    = ThreeDoubleQuotes Newline? MultilineBasicText* ThreeDoubleQuotes

MultilineBasicText
    = MultilineBasicCharacter
    / Backslash Newline
    / Newline

MultilineBasicCharacter
    = !ThreeDoubleQuotes MultilineNormalCharacter
    / EscapedCharacter

MultilineNormalCharacter                        "NormalCharacter"
    = !Newline [^\\]

MultilineLiteralString
    = ThreeSingleQuotes Newline? MultilineLiteralText* ThreeSingleQuotes

MultilineLiteralText
    = !"'''" MultilineLiteralCharacter
    / Newline

MultilineLiteralCharacter                       "AnyCharacter"
    = !Newline .

Boolean
    = "true"
    / "false"

Float
    = Integer ( Fraction Exponent? / Exponent )

Fraction
    = "." Digit ( "_"? Digit )*

Exponent
    = ( "e" / "E" ) Integer

Integer
    = Sign? IntDigits

Sign
    = "+"
    / "-"

IntDigits
    = Digit_1to9 ( "_"? Digit )+
    / Digit

Digit_1to9
    = [1-9]

Digit
    = [0-9]

DateTime
    = FullDate "T" FullTime

FullDate                                        "FullDate (YYYY-mm-dd)"
    = Year "-" Month "-" MDay

Year
    = Digit Digit Digit Digit

Month
    = Digit Digit

MDay
    = Digit Digit

FullTime
    = Time TimeOffset?

Time                                            "Time (HH:MM:SS[.sss])"
    = Hour ":" Minute ":" Second SecondFraction?

Hour
    = Digit Digit

Minute
    = Digit Digit

Second
    = Digit Digit

SecondFraction
    = "." Digit+

TimeOffset                                      "TimeOffset (Z or +/-HH:MM)"
    = "Z"
    / Sign Hour ":" Minute

Array
    = "[" ArraySpace*
          ( ArrayValue
            ArraySpace*
            ( "," ArraySpace* )? )? "]"

ArrayValue
    = Value ( ArraySpace* "," ArraySpace* ArrayValue )?

ArraySpace
    = Whitespace
    / Newline
    / Comment

InlineTable
    = "{" Whitespace*
          ( KeyValue
            ( Whitespace* "," Whitespace* KeyValue )*
            Whitespace* )? "}"

TableArrayHeader
    = "[" TableHeader "]"

TableHeader
    = "[" Whitespace*
          Key
          ( Whitespace* "." Whitespace* Key )*
          Whitespace* "]"
